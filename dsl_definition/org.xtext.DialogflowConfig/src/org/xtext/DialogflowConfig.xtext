grammar org.xtext.DialogflowConfig with org.eclipse.xtext.common.Terminals

generate dialogflowConfig "http://www.xtext.org/DialogflowConfig"

Agent:
    'Agent' name=ID
    'language' language=Language
    ('description' description=STRING)?
    ('version' version=VERSION)?
    ('ml_classification_threshold' mlMinConfidence=DOUBLE)?
    webhook=Webhook?
    elements+=AbstractElement*
    interactionLogs?='enable_logs'?
    & stackdriverLogs?='log_to_google_cloud'?
    & isPrivate?='private'?
    & hybridMatchMode?='hybrid_match_mode'?
    'end';

terminal DOUBLE:
    INT '.' INT;
    
terminal VERSION:
    INT '.' INT '.' INT;

AbstractElement:
    Intent | EntityType;

Intent:
    fallback?='fallback'?
    'Intent' name=ID
    ('contexts' (('in' inputContexts+=InputContext+)?
    ('out' affectedContexts+=OutputContext+)?))?
    ('parameters' parameters+=Parameter*)?
    ('trained' 'with' trainingPhrases+=TrainingPhrase (',' trainingPhrases+=TrainingPhrase)*
        | 'file' file=STRING
    )?
    ('response' responses+=STRING+)?
    ('action' action=STRING)?
    ('events' events+=STRING+)?
    (webHookFulfillment?='webhook_fullfillment'?
    & webHookForSlotFilling?='webhook_slot_filling'?
    & disable_ml?='disable_ml'?
    & end?='end_of_conversation'?)
    'end';

InputContext:
    type=[EntityType];

OutputContext:
    type=[EntityType] ('lifespan' lifespan=INT)?;

TrainingPhrase:
    data+=AbstractWord+;

AbstractWord:
    Text | customToken | builtinToken;

Text:
    text=STRING;

customToken:
    type=[EntityType];

builtinToken:
    type=BuiltInType
;

Parameter:
    (type=[EntityType] | builtInType=BuiltInType)
    ('('
    (required?='required'? & ('prompts' prompts+=STRING+)? & list?='list'?)
    ')')?;

EntityType:
    'Type' name=ID
    (dynamic?='dynamic' |
    'values' values+=Entity+)
    ('state'
    (isOverridable?='overridable'?
    & isEnum?='enum'?
    & automatedExpansion?='auto_expand'?
    & allowFuzzyExtraction?='fuzzy_extract'?))?
    'end';

Entity:
    name=STRING
    ('(' synonyms+=STRING* ')')?;

Webhook:
    'Webhook'
    available?='active'?
    ('url' url=STRING)
    ('headers' headers+=Header+)?;

Header:
    key=STRING ':' value=STRING;

enum Language:
    en |
    de |
    fr |
    es |
    da |
    hi |
    id |
    it |
    ja |
    ko |
    ni |
    pl |
    pt |
    ru |
    sv |
    th |
    tr |
    uk;
    
 enum BuiltInType:
    date_time
    | date
    | date_period
    | time
    | time_period
    | number
    | cardinal
    | ordinal
    | number_integer
    | number_sequence
    | flight_number
    | unit_area
    | unit_currency
    | unit_length
    | unit_speed
    | unit_volume
    | unit_weight
    | unit_information
    | percentage
    | temperature
    | duration
    | age
    | currency_name
    | unit_area_name
    | unit_length_name
    | unit_speed_name
    | unit_volume_name
    | unit_weight_name
    | unit_information_name
    | address
    | zip_code
    | geo_capital
    | geo_country
    | geo_country_code
    | geo_city
    | geo_state
    | place_attraction
    | airport
    | location
    | email
    | phone_number
    | given_name
    | last_name
    | person
    | music_artist
    | music_genre
    | color
    | language
    | any
    | url
 ;