grammar org.xtext.DialogflowConfig with org.eclipse.xtext.common.Terminals

generate dialogflowConfig "http://www.xtext.org/DialogflowConfig"

Agent:
	'Agent' name=ID
	elements+=AbstractElement*
;

AbstractElement:
	Intent | EntityType
;

Intent:
	'Intent' name=ID
	('parameters' parameters+=Parameter*)?
	('contexts' (
		('in' inputContexts+=InputContext+)?
		('out' affectedContexts+=OutputContext+)?
	))?
	(
		'file' file=STRING 
		| 'trained' 'with' trainingPhrases+=TrainingPhrase (',' trainingPhrases+=TrainingPhrase)*
	)?
;

InputContext:
	type=[EntityType]
;
OutputContext:
	type=[EntityType]('lifespan'lifespan=INT)?
;

TrainingPhrase:
	data+=AbstractWord+
;

AbstractWord:
	Text | Token
;

Text:
	text=STRING
;

Token:
	type=[EntityType]
;

Parameter:
	type=[EntityType] 
	('('
		(required?='required'? & list?='list'?)
	')')?
;

EntityType:
	'Type' name=ID
	(dynamic?='dynamic' | builtIn?='builtin' |
	'values' values+=Entity+)
	('state' 
		isOverridable?='overridable'?
		&isEnum?='enum'?
		&automatedExpansion?='autoexpand'?
		&allowFuzzyExtraction?='fuzzyextract'?
	)?
;

Entity:
	name=STRING
	('('synonyms+=STRING*')')?
;
