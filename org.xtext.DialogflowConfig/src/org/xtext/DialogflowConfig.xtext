grammar org.xtext.DialogflowConfig with org.eclipse.xtext.common.Terminals

generate dialogflowConfig "http://www.xtext.org/DialogflowConfig"

Agent:
	'Agent' name=ID
	('config' 
		('language' language=Language)?
		&('something')?
	)?
	elements+=AbstractElement*
;

enum Language:
	en |
	de |
	fr |
	es
;

AbstractElement:
	Intent | EntityType
;

Intent:
	'Intent' name=ID
	('parameters' parameters+=Parameter*)?
	('contexts' (
		('in' inputContexts+=InputContext+)?
		('out' affectedContexts+=OutputContext+)?
	))?
	(
		'file' file=STRING 
		| 'trained' 'with' trainingPhrases+=TrainingPhrase (',' trainingPhrases+=TrainingPhrase)*
	)?
	('response' responses+=STRING+)?
	(webHook?='webhook'?&webHookForSlotFilling?='webhook_for_slot_filling'?)
;

InputContext:
	type=[EntityType]
;
OutputContext:
	type=[EntityType]('lifespan'lifespan=INT)?
;

TrainingPhrase:
	data+=AbstractWord+
;

AbstractWord:
	Text | Token
;

Text:
	text=STRING
;

Token:
	type=[EntityType]
;

Parameter:
	type=[EntityType] 
	('('
		(required?='required'? & list?='list'?)
	')')?
;

EntityType:
	'Type' name=ID
	(dynamic?='dynamic' | builtIn?='builtin' |
	'values' values+=Entity+)
	('state' 
		isOverridable?='overridable'?
		&isEnum?='enum'?
		&automatedExpansion?='auto_expand'?
		&allowFuzzyExtraction?='fuzzy_extract'?
	)?
;

Entity:
	name=STRING
	('('synonyms+=STRING*')')?
;
